// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// ----------------------
// Data types
// ----------------------
struct Ray
{
    float3 origin;
    float3 direction;
    float3 energy;
};

struct RayHit
{
    float3 position;
    float distance;
    float3 normal;
    float3 albedo;
    float3 specular;
    float smoothness;
    float3 emission;
};

struct Sphere
{
    float3 position;
    float radius;
    float3 color;
    float3 specular;
    float smoothness;
    float3 emission;
};

static const float PI = 3.14159265f;

// ----------------------
// Parameters
// ----------------------

RWTexture2D<float4> Result;

float4x4 CameraToWorld;
float4x4 CameraInverseProjection;

Texture2D<float4> SkyTexture;
SamplerState sampler_SkyTexture;
float SkyIntensity;

StructuredBuffer<Sphere> Spheres;

int MaxBounce;

float2 PixelOffset;
float4 DirectionalLight;
float4 GroundColor;

float2 Pixel;
float Seed;



// ----------------------
// Functions
// ----------------------

float rand()
{
    float result = frac(sin(Seed / 100.0f * dot(Pixel, float2(12.9898f, 78.233f))) * 43758.5453f);
    Seed += 1.0f;
    return result;
}

float sdot(float3 x, float3 y, float f = 1.0f)
{
    return saturate(dot(x, y) * f);
}

float energy(float3 color)
{
    return dot(color, 1.0f / 3.0f);
}

float SmoothnessToPhongAlpha(float s)
{
    return pow(1000.0f, s * s);
}

float3x3 GetTangentSpace(float3 normal)
{
    // Choose a helper vector for the cross product
    float3 helper = float3(1, 0, 0);
    if (abs(normal.x) > 0.99f)
        helper = float3(0, 0, 1);
    // Generate vectors
    float3 tangent = normalize(cross(normal, helper));
    float3 binormal = normalize(cross(normal, tangent));
    return float3x3(tangent, binormal, normal);
}

float3 SampleHemisphere(float3 normal, float alpha)
{
    // Sample the hemisphere, where alpha determines the kind of the sampling
    float cosTheta = pow(rand(), 1.0f / (alpha + 1.0f));
    float sinTheta = sqrt(1.0f - cosTheta * cosTheta);
    float phi = 2 * PI * rand();
    float3 tangentSpaceDir = float3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);
    // Transform direction to world space
    return mul(tangentSpaceDir, GetTangentSpace(normal));
}

Ray CreateRay(float3 origin, float3 direction) 
{
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    ray.energy = float3(1, 1, 1);
    return ray;
}

Ray CreateCameraRay(float2 uv)
{
    // Transform the camera origin to world space
    float3 origin = mul(CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    
    // Invert the perspective projection of the view-space position
    float3 direction = mul(CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    
    // Transform the direction from camera to world space and normalize
    direction = mul(CameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);
    
    return CreateRay(origin, direction);
}

RayHit CreateRayHit()
{
    RayHit hit;
    hit.position = float3(0.0f, 0.0f, 0.0f);
    hit.distance = 1.#INF;
    hit.normal = float3(0.0f, 0.0f, 0.0f);
    hit.albedo = float3(0, 0, 0);
    hit.specular = 0;
    hit.emission = float3(0, 0, 0);
    hit.smoothness = 0;
    return hit;
}

void IntersectGroundPlane(Ray ray, inout RayHit bestHit)
{
    // Calculate distance along the ray where the ground plane is intersected
    float t = -ray.origin.y / ray.direction.y;
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = float3(0.0f, 1.0f, 0.0f);
        bestHit.specular = GroundColor.a;
        bestHit.albedo = GroundColor.xyz;
        bestHit.smoothness = 0.5f;
        bestHit.emission = float3(0, 0, 0);
    }
}

void IntersectSphere(Ray ray, inout RayHit bestHit, Sphere sphere)
{
    // Calculate distance along the ray where the sphere is intersected
    float3 d = ray.origin - sphere.position;
    float p1 = -dot(ray.direction, d);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.radius * sphere.radius;
    if (p2sqr < 0)
        return;
    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < bestHit.distance)
    {
        bestHit.distance = t;
        bestHit.position = ray.origin + t * ray.direction;
        bestHit.normal = normalize(bestHit.position - sphere.position);
        bestHit.albedo = sphere.color;
        bestHit.specular = sphere.specular;
        bestHit.smoothness = sphere.smoothness;
        bestHit.emission = sphere.emission;
    }
}

float3 GetSkyColor(Ray ray)
{
    float theta = acos(ray.direction.y) / -PI;
    float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.5f;
    return SkyTexture.SampleLevel(sampler_SkyTexture, float2(phi, theta), 0).xyz;
}

RayHit Trace(Ray ray)
{
    RayHit bestHit = CreateRayHit();
    
    uint numSpheres, stride;
    Spheres.GetDimensions(numSpheres, stride);

    for (uint i = 0; i < numSpheres; i++)
    {
        IntersectSphere(ray, bestHit, Spheres[i]);
    }
    IntersectGroundPlane(ray, bestHit);
    
    return bestHit;
}

float3 Shade(inout Ray ray, RayHit hit)
{
    if (hit.distance < 1.#INF)
    {
        /*float3 specular = hit.specular;
        float3 albedo = hit.albedo;
        
        // Reflect the ray and multiply energy with specular reflection
        ray.origin = hit.position + hit.normal * 0.001f; // bounce a little before reflection to avoid being stuck inside object
        ray.direction = reflect(ray.direction, hit.normal);
        ray.energy *= specular;
        
        // Shadow test ray
        bool shadow = false;
        Ray shadowRay = CreateRay(hit.position + hit.normal * 0.001f, -1 * DirectionalLight.xyz);
        RayHit shadowHit = Trace(shadowRay);
        if (shadowHit.distance != 1.#INF)
        {
            return float3(0.0f, 0.0f, 0.0f);
        }

        return saturate(dot(hit.normal, DirectionalLight.xyz) * -1) * DirectionalLight.w * albedo;  */
        
        // Calculate chances of diffuse and specular reflection
        hit.albedo = min(1.0f - hit.specular, hit.albedo);
        float specChance = energy(hit.specular);
        float diffChance = energy(hit.albedo);
        float sum = specChance + diffChance;
        specChance /= sum;
        diffChance /= sum;
        // Roulette-select the ray's path
        float roulette = rand();
        if (roulette < specChance)
        {
            // Specular reflection
            float alpha = SmoothnessToPhongAlpha(hit.smoothness);
            ray.origin = hit.position + hit.normal * 0.001f;
            ray.direction = SampleHemisphere(reflect(ray.direction, hit.normal), alpha);
            float f = (alpha + 2) / (alpha + 1);
            ray.energy *= (1.0f / specChance) * hit.specular * sdot(hit.normal, ray.direction, f);
        }
        else
        {
            // Diffuse reflection
            ray.origin = hit.position + hit.normal * 0.001f;
            ray.direction = SampleHemisphere(hit.normal, 1.0f);
            ray.energy *= (1.0f / diffChance) * hit.albedo;
        }
        return hit.emission;
    }
    else
    {
        // Erase the ray's energy - the sky doesn't reflect anything
        ray.energy = 0.0f;
        // Sample the skybox and write it
        float theta = acos(ray.direction.y) / -PI;
        float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.5f;
        float4 skyColor = SkyTexture.SampleLevel(sampler_SkyTexture, float2(phi, theta), 0);
        return skyColor.xyz * SkyIntensity;
    }
}

// ----------------------
// Kernel
// ----------------------

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    // Get the dimensions of the RenderTexture
    uint width, height;
    Result.GetDimensions(width, height);
    
    Pixel = id.xy;
    
    // transfrom pixel to -1, 1 range
    float2 uv = float2((id.x + PixelOffset.x) / width * 2.0f - 1.0f, (id.y + PixelOffset.y) / height * 2.0f - 1.0f);
    
    // get camera ray for pixel
    Ray ray = CreateCameraRay(uv);
    
    // do ray trace
    float3 result = float3(0, 0, 0);
    for (int i = 0; i < MaxBounce; i++)
    {
        RayHit hit = Trace(ray);
        result += ray.energy * Shade(ray, hit);
        if (!any(ray.energy))
            break;
    }
    
    Result[id.xy] = float4(result, 0);
 }
